---
sidebar_position: 8
---

# Задача ОУ-1 Учет по партиям

## Условие
Компания занимается оптовой торговлей. 
- Поступление товара отражается документом «Приходная накладная», 
- продажа – «Расходная накладная». 

Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Учет товаров ведется в разрезе складов. Поступление и продажа осуществляется с указанием склада (в шапке документа).

При проведении документа «Расходная накладная» необходимо производить списание товара со склада. В том случае, когда товара не хватает, документ проводиться не должен.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от метода списания (FIFO или LIFO), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения. 

## Решение 

### Обработка проведения Расходной накладной

```
Процедура ОбработкаПроведения(Отказ, Режим)
   	Движения.ОстаткиНоменклатуры.Записывать = Истина;

	// ++ 1. ОЧИСТКА ДВИЖЕНИЙ
	Движения.ОстаткиНоменклатуры.Записать();
	// -- 1.
	
	// ++ 2. БЛОКИРОВКА
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	// -- 2.   
	
	// ++ 3. ЧТЕНИЕ ДАННЫХ
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЧ.Номенклатура КАК Номенклатура,
		|	ТЧ.Количество КАК КоличествоТЧ,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ТЧ.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	ТЧ КАК ТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Склад = &Склад
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							ТЧ.Номенклатура КАК Номенклатура
		|						ИЗ
		|							ТЧ КАК ТЧ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(КоличествоТЧ),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Товар", Перечисления.ВидыНоменклатуры.Товар);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	УчетнаяПолитика = УчетнаяПолитика();
	Если УчетнаяПолитика = Неопределено Тогда
		Сообщить("Не задана учетная политика");	
		Отказ = Истина;
	КонецЕсли;
	
	Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ФИФО Тогда  
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	// -- 3. 
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		// ++ 4. КОНТРОЛЬ
		Если ВыборкаНоменклатура.КоличествоТЧ > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщить("По товару " + ВыборкаНоменклатура.НоменклатураПредставление 
			   + " не хватает " + (ВыборкаНоменклатура.КоличествоТЧ - ВыборкаНоменклатура.КоличествоОстаток));
			Отказ = Истина;
		КонецЕсли;
		// -- 4.  
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		// ++ 5. ФОРМИРОВАНИЕ ДВИЖЕНИЙ 
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоТЧ;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() И ОсталосьСписать > 0 Цикл
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Склад = Склад;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			
			Движение.Количество = ОсталосьСписать;
			Если ВыборкаДетальныеЗаписи.КоличествоОстаток <= ОсталосьСписать Тогда
				Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток; 
			КонецЕсли;
			ОсталосьСписать = ОсталосьСписать - Движение.Количество;
			
			Если ВыборкаДетальныеЗаписи.КоличествоОстаток >0 Тогда 
			Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток*Движение.Количество
				/ВыборкаДетальныеЗаписи.КоличествоОстаток; 
			КонецЕсли;
		КонецЦикла; 
		// -- 5. 
		
	КонецЦикла;
	

	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
```


